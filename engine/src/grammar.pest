WHITESPACE = _{ " " | "\t" }


endline = _{ "\n" | "\r\n" | "\r" }
el = {"\n" | "\r\n" | "\r" }
line = @{ (!endline ~ ANY)+ }
emptyline = _{ WHITESPACE* ~ endline }
attribute = ${":" ~ ASCII_ALPHANUMERIC+ ~ WHITESPACE+ ~ line ~ endline}

lang = ${ (!endline ~ !WHITESPACE ~ ANY)+ }
codeblockbegin = @{ "```" ~ WHITESPACE* ~ lang? ~ WHITESPACE* }
codeblockend = @{ "```" ~ WHITESPACE* }
codeblockescape = @{"\\```" ~ (!endline ~ ANY)*}

codeblock = {
	codeblockbegin ~ endline
	~ (((codeblockescape | (!codeblockend ~ line)) ~ el) | (WHITESPACE* ~ el))*
	~ codeblockend
}

special_start = {
	emptyline | codeblockbegin | WHITESPACE* ~ "#" | "==" | WHITESPACE* ~ "*"
}

paragraphline = @{
	!special_start ~ line
}
paragraph = @{
	WHITESPACE* ~ paragraphline ~ (endline ~ paragraphline)*
}

extsymbol = @{ ASCII_ALPHANUMERIC+ }

extblock = ${
	"==[" ~ extsymbol ~ "]" ~ WHITESPACE* ~ endline
	~ ((emptyline | codeblock | ul3 | ul2 | ul1 | paragraph) ~ endline)*
	~ "=="
}

h1 = ${
	"#" ~ paragraph ~ (emptyline* ~(codeblock | extblock | ul3 | ul2 | ul1 | h6 | h5 | h4 | h3 | h2 | paragraph))*
}

h2 = {
	"##" ~ paragraph ~ (emptyline* ~(codeblock | extblock | ul3 | ul2 | ul1  | h6 | h5 | h4 | h3 | paragraph))*
}

h3 = {
	"###" ~ paragraph ~ (emptyline* ~(codeblock | extblock | ul3 | ul2 | ul1  | h6 | h5 | h4 | paragraph))*
}


h4 = {
	"####" ~ paragraph ~ (emptyline* ~(codeblock  | extblock | ul3 | ul2 | ul1 | h6 | h5 |  paragraph))*
}


h5 = {
	"#####" ~ paragraph ~ (emptyline* ~(codeblock  | extblock | ul3 | ul2 | ul1 | h6 | paragraph))*
}


h6 = {
	"######" ~ paragraph ~ (emptyline* ~(codeblock | extblock | ul3 | ul2 | ul1 | paragraph))*
}

dummyline = {
	WHITESPACE* ~ endline
}

ul1 = {
	WHITESPACE* ~ "*" ~ (codeblock | extblock | paragraph | dummyline) ~ (endline ~ (ul3 | ul2 | ul1))*
}

ul2 = {
	WHITESPACE* ~ "**" ~ (codeblock | extblock | paragraph | dummyline) ~ (endline ~ (ul3 | ul2))*
}

ul3 = {
	WHITESPACE* ~ "***" ~ (codeblock | extblock | paragraph | dummyline) ~ (endline ~ (ul3))*
}

main = { SOI ~ (attribute | emptyline)* ~ "::" ~ "\n" ~ emptyline* ~ h1 ~ endline? ~ EOI }
