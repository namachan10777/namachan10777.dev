WHITESPACE = _{ " " | "\t" }

endline = _{ "\n" | "\r\n" | "\r" }
line = @{ (!endline ~ ANY)+ }
emptyline = _{ endline }

lang = ${ (!endline ~ !WHITESPACE ~ ANY)+ }
codeblockbegin = @{ "```" ~ WHITESPACE* ~ lang? ~ WHITESPACE* }
codeblockend = @{ "```" }
codeblockescape = @{"\\```" ~ (!endline ~ ANY)*}

codeblock = {
	codeblockbegin ~ endline
	~ ((codeblockescape | !codeblockend ~ line) ~ endline)*
	~ codeblockend
}

special_start = {
	emptyline | codeblockbegin | WHITESPACE* ~ "#" | "=="
}

paragraphline = @{
	!special_start ~ line
}
paragraph = @{
	WHITESPACE* ~ paragraphline ~ (endline ~ paragraphline)*
}

extsymbol = @{ ASCII_ALPHANUMERIC+ }

extblock = ${
	"==[" ~ extsymbol ~ "]" ~ WHITESPACE* ~ endline
	~ ((emptyline | codeblock | paragraph) ~ endline)*
	~ "=="
}

h1 = ${
	"#" ~ paragraph ~ (emptyline* ~(codeblock | extblock | h6 | h5 | h4 | h3 | h2 | paragraph))*
}

h2 = {
	"##" ~ paragraph ~ (emptyline* ~(codeblock | extblock  | h6 | h5 | h4 | h3 | paragraph))*
}

h3 = {
	"###" ~ paragraph ~ (emptyline* ~(codeblock | extblock  | h6 | h5 | h4 | paragraph))*
}


h4 = {
	"####" ~ paragraph ~ (emptyline* ~(codeblock  | extblock | h6 | h5 |  paragraph))*
}


h5 = {
	"#####" ~ paragraph ~ (emptyline* ~(codeblock  | extblock | h6 | paragraph))*
}


h6 = {
	"######" ~ paragraph ~ (emptyline* ~(codeblock | extblock  | paragraph))*
}

main = { SOI ~ h1 ~ endline? ~ EOI }
