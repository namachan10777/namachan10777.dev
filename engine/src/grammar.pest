WHITESPACE = _{ " " | "\t" }


el = _{ "\n" | "\r\n" | "\r"}
visibleel = { "\n" | "\r\n" | "\r"}
line = @{ (!el ~ ANY)+ ~ el }
emptyline = _{ WHITESPACE* ~ el }
dummyline = @{ WHITESPACE* ~ el }
attribute = ${":" ~ ASCII_ALPHANUMERIC+ ~ WHITESPACE+ ~ line}
lang = @{ (!el ~ !WHITESPACE ~ ANY)+ ~ el }
codeblockbegin = { "```" ~ WHITESPACE* ~ (emptyline | lang?) }
codeblockend = { "```" ~ WHITESPACE* ~ el }
escape = _{ "\\" }
codeblockescape = @{escape ~ "\\"* ~ "```" ~ WHITESPACE* ~ line }
headingescape = @{escape ~ "\\"* ~ "#" ~ line}
listescape = @{escape ~ "\\"* ~ "*" ~ line}

codeblock = ${
	codeblockbegin
	~ (!codeblockend ~ (codeblockescape | line | dummyline))*
	~ codeblockend
}

special_start = {
	codeblockbegin | WHITESPACE* ~ "#" | "==" | WHITESPACE* ~ "*"
}

paragraphline = ${
	WHITESPACE* ~ !special_start ~ (codeblockescape | headingescape | listescape)? ~ line
}

paragraph = ${
	paragraphline+
}

extsymbol = @{ ASCII_ALPHANUMERIC+ }

extblock = {
	"==[" ~ extsymbol ~ "]" ~ WHITESPACE* ~ el
	~ (emptyline | codeblock | ul3 | ul2 | ul1 | paragraph)*
	~ "=="
}

h1 = ${
	WHITESPACE* ~ "#" ~ paragraph ~  (emptyline | codeblock | extblock | h6 | h5 | h4 | h3 | h2 | paragraph)*
}

h2 = ${
	WHITESPACE* ~ "##" ~ paragraph ~ (emptyline | codeblock | extblock | ul3 | ul2 | ul1  | h6 | h5 | h4 | h3 | paragraph)*
}

h3 = ${
	WHITESPACE* ~ "###" ~ paragraph ~ (emptyline | codeblock | extblock | ul3 | ul2 | ul1  | h6 | h5 | h4 | paragraph)*
}

h4 = ${
	WHITESPACE* ~ "####" ~ paragraph ~ (emptyline | codeblock  | extblock | ul3 | ul2 | ul1 | h6 | h5 |  paragraph)*
}

h5 = ${
	WHITESPACE* ~ "#####" ~ paragraph ~ (emptyline | codeblock  | extblock | ul3 | ul2 | ul1 | h6 | paragraph)*
}

h6 = ${
	WHITESPACE* ~ "######" ~ paragraph ~ (emptyline | codeblock | extblock | ul3 | ul2 | ul1 | paragraph)*
}

ul1 = ${
	li1+
}

ul2 = ${
	li2+
}

ul3 = ${
	li3+
}

li1 = ${
	WHITESPACE* ~ "*" ~ (dummyline | paragraph) ~ (ul3 | ul2)*
}

li2 = ${
	WHITESPACE* ~ "**" ~ (dummyline | paragraph) ~ (ul3)*
}

li3 = ${
	WHITESPACE* ~ "***" ~ (dummyline | paragraph)
}

attributes = {
	(attribute | emptyline)* ~ "::" ~ el
}

main = { SOI ~ attributes ~ emptyline* ~ h1 ~ el? }
