WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

floating = @{
	("+" | "-")?
	~ (
		(ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) |
		(ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+) |
		(ASCII_DIGIT? ~ "." ~ ASCII_DIGIT*) ~ "e" ~ ASCII_DIGIT+
	)
}

hexint = @{
	("+" | "-")?
	~ ("0x" ~ ASCII_HEX_DIGIT+)
}

binint = @{
	("+" | "-")?
	~ ("0b" ~ ASCII_BIN_DIGIT+)
}

octint = @{
	("+" | "-")?
	~ ("0h" ~ ASCII_OCT_DIGIT+)
}

digint = @{
	("+" | "-")?
	~ (ASCII_DIGIT+)
}

inline_string = @{ "`" ~ raw_inline_string ~ "`" }
raw_inline_string = @{ ("\\\\" | "\\`" | !"`" ~ ANY)* }

block_string = @{ "```" ~ raw_block_string ~ "```" }
raw_block_string = @{ ("\\\\" | "\\`" | !"```" ~ ANY)* }

plain = @{ (raw_text | escape)+ }
raw_text = @{ !("\\" | "}") ~ ANY }
escape = @{ ("\\}" | "\\\\") }
text = ${ "{" ~ (command | plain)* ~ "}" }

symbol = @{ ASCII_ALPHANUMERIC+ }

command_name = ${ "\\" ~ symbol }
command = !{ command_name ~ (block_string | inline_string | hexint | binint | octint | digint | floating | text)* ~ ";" }

main = { SOI ~ command ~ EOI }
