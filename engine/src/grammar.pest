WHITESPACE  = _{ " " | "\t" | "\n" | "\r" }

symbol        = @{ ASCII_ALPHA ~ ("_" | "-" | ASCII_ALPHANUMERIC)* }
cmd_name      = ${ "\\" ~ symbol }
attribute     =  { symbol ~ "=" ~ value }
attributes    =  { attribute* }
end_of_cmd    =  { ";" }
cmd           =  !{ cmd_name ~ attributes ~ (end_of_cmd | text | cmds) }
esc_dq_str    = @{ "\\\"" }
esc_esc_str   = @{ "\\\\" }
char_in_str   =  { !"\"" ~ ANY }
str           = ${ "\"" ~ (esc_dq_str | esc_esc_str | char_in_str)* ~ "\"" }
blockstr      = ${ "###`" ~ (!"`###" ~ ANY)* ~ "`###" }
inlinestr     = ${ "`" ~ ("\\`" | (!"`" ~ ANY))* ~ "`" }
esc_endbrace  = @{ "\\}" }
esc_esc       = @{ "\\\\" }
char_in_text  = @{ ANY }
text          = ${
	"{" ~
	(!"}" ~ (esc_esc | esc_endbrace | cmd | inlinestr | char_in_text))*
	~ "}"
}
cmds          =  { "[" ~ cmd* ~ "]" }
int           = @{ ASCII_DIGIT+ }
float         = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
value         =  { str | text | float | int | blockstr | list }
list          =  { "(" ~ (value ~ ("," ~ value)* )? ~ ")" }

main = { SOI ~ cmd ~ EOI }
