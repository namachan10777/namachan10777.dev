WHITESPACE = _{ " " | "\t" }
white = { " " | "\t" }


el = _{ "\n" | "\r\n" | "\r"}
visibleel = { "\n" | "\r\n" | "\r"}
plain = @{
	(!el ~ !inline_escapes ~ !"]" ~ !"[" ~ !"$" ~ ANY)+
}
line = ${
	(
		white* ~ block_escapes ~ inlinetext?
		| inlinetext
	) ~ visibleel
	}
emptyline = _{ WHITESPACE* ~ el }
dummyline = @{ WHITESPACE* ~ el }
attribute = ${":" ~ ASCII_ALPHANUMERIC+ ~ WHITESPACE+ ~ line}
lang = @{ (!el ~ !WHITESPACE ~ ANY)+ ~ el }
codeblockbegin = { "```" ~ WHITESPACE* ~ (emptyline | lang?) }
codeblockend = { "```" ~ WHITESPACE* ~ el }
escape = _{ "\\" }

escaped_codeblock = @{escape ~ "\\"* ~ "```" }
escaped_heading = @{escape ~ "\\"* ~ "#"+ }
escaped_li = @{escape ~ "\\"* ~ "*"+ }

escaped_sqbra_begin = @{ escape ~ "\\"* ~ "[" }
escaped_sqbra_end = @{ escape ~ "\\"* ~ "]" }
escaped_ex = @{ escape ~ "\\"* ~ "!" }
escaped_ext = @{ escape ~ "\\"* ~ "$" }

extname = @{
	ASCII_ALPHANUMERIC+
}
extinner = @{
	(!"]" ~ ANY)*
}
inline_ext = ${
	"$" ~ extname ~ "[" ~ extinner ~ "]"
}

url = @{
	(!")" ~ ANY)*
}

inline_escapes = @{
	escaped_sqbra_begin
	| escaped_sqbra_end
	| escaped_ext
}

block_escapes = @{
	escaped_codeblock
	| escaped_heading
	| escaped_li
}

inlinetext = ${
	(
		!"]" ~ 
		!el ~
		( inline_escapes
		| img
		| link
		| inline_ext
		| plain
		)
	)+
}

alttxt = @{
	(!"]" ~ ANY)*
}

img = ${
	"![" ~ alttxt ~ "](" ~ url ~ ")"
}

linktext = ${
	inlinetext*
}

link = ${
	"[" ~ linktext ~ "](" ~ url ~ ")"
}

codeline = @{
	(!el ~ ANY)*
}

codeblock = ${
	codeblockbegin
	~ (!codeblockend ~ (white* ~ escaped_codeblock ~ codeline | codeline) ~ visibleel)*
	~ codeblockend
}

block_start = @{
	"```" | "#" | "==" | "*"
}

inline_start = @{
	"![" | "[" | "`"
}

paragraph = ${
	(!(WHITESPACE* ~ block_start) ~ line)+
}

extsymbol = @{ ASCII_ALPHANUMERIC+ }

extblock = {
	"==[" ~ extsymbol ~ "]" ~ WHITESPACE* ~ el
	~ (emptyline | codeblock | ul3 | ul2 | ul1 | paragraph)*
	~ "=="
}

h1 = ${
	WHITESPACE* ~ "#" ~ paragraph ~  (emptyline | codeblock | extblock | h6 | h5 | h4 | h3 | h2 | paragraph)*
}

h2 = ${
	WHITESPACE* ~ "##" ~ paragraph ~ (emptyline | codeblock | extblock | ul3 | ul2 | ul1  | h6 | h5 | h4 | h3 | paragraph)*
}

h3 = ${
	WHITESPACE* ~ "###" ~ paragraph ~ (emptyline | codeblock | extblock | ul3 | ul2 | ul1  | h6 | h5 | h4 | paragraph)*
}

h4 = ${
	WHITESPACE* ~ "####" ~ paragraph ~ (emptyline | codeblock  | extblock | ul3 | ul2 | ul1 | h6 | h5 |  paragraph)*
}

h5 = ${
	WHITESPACE* ~ "#####" ~ paragraph ~ (emptyline | codeblock  | extblock | ul3 | ul2 | ul1 | h6 | paragraph)*
}

h6 = ${
	WHITESPACE* ~ "######" ~ paragraph ~ (emptyline | codeblock | extblock | ul3 | ul2 | ul1 | paragraph)*
}

ul1 = ${
	li1+
}

ul2 = ${
	li2+
}

ul3 = ${
	li3+
}

li1 = ${
	WHITESPACE* ~ "*" ~ (dummyline | paragraph) ~ (ul3 | ul2)*
}

li2 = ${
	WHITESPACE* ~ "**" ~ (dummyline | paragraph) ~ (ul3)*
}

li3 = ${
	WHITESPACE* ~ "***" ~ (dummyline | paragraph)
}

attributes = {
	(attribute | emptyline)* ~ "::" ~ el
}

top_block = {
	h6 | h5 | h4 | h3 | h2 | h1 | li3 | li2 | li1 | extblock| codeblock | paragraph
}

main = { SOI ~ attributes ~ emptyline* ~ top_block+ ~ el? ~ EOI }
